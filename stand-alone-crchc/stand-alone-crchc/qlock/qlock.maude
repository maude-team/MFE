----------------------
--- QLOCK protocol ---
----------------------
--- taken from
--- http://www.jaist.ac.jp/~kokichi/class/i613-0912/cafeobjCode/qlockPS/index.html
set include BOOL off .
set include TRUTH-VALUE on .

fmod QLOCK-SYNTAX is
  sort Nat .
  op 0 : -> Nat [ctor] .
  op s_ : Nat -> Nat [ctor] .

  sort Soup .
  subsort Nat < Soup .
  op none : -> Soup [ctor] .
  op __ : Soup Soup -> Soup [ctor assoc comm id: none] .

  sort Queue .
  op mt : -> Queue [ctor] .
  op _@_ : Nat Queue -> Queue [ctor] .
  op _;_ : Queue Queue -> Queue .
  eq mt ; Q:Queue = Q:Queue .
  eq (N:Nat @ Q1:Queue) ; Q2:Queue = N:Nat @ (Q1:Queue ; Q2:Queue) .

  sort State .
  op _|_|_|_ : Soup Soup Soup Queue -> State [ctor] .
endfm

mod QLOCK is
  pr QLOCK-SYNTAX .

  vars Si Sw Sc : Soup .   var Q : Queue .   vars N N' : Nat .

  rl Si N | Sw | Sc | Q => Si | Sw N | Sc | Q ; (N @ mt ) .

  rl Si | Sw N | Sc | N @ Q => Si | Sw | Sc N | N @ Q .

  rl Si | Sw | Sc N | N' @ Q => Si N | Sw | Sc | Q .
endm

fmod QLOCK-PROPS is
  pr QLOCK-SYNTAX .
  pr BOOL-OPS .

  vars Si Sw Sc : Soup .    var Q : Queue .    vars N N' : Nat .

--- State predicates
 ops init mutex aux : State -> [Bool] .

--- Initial states
 ceq [init] : init( Si | none | none | mt ) = true if set?(Si) .

--- Mutual exclusion property
  eq [1] : mutex( Si | Sw | none | Q ) = true .
  eq [2] : mutex( Si | Sw | N | N @ Q ) = true .
  eq [3] : mutex( Si | Sw | N N' Sc | Q ) = false .

--- Auxiliary invariant
 ceq [4] : aux( Si | Sw | none | Q ) = true if set?(Si Sw) .
 ceq [5] : aux( Si | Sw | N | N @ Q ) = true if set?(Si Sw N) .
  eq [6] : aux( Si | Sw | N N' Sc | Q ) = false .

--- Auxiliary predicates
  op equ : Nat Nat -> Bool [comm] .
  op in? : Nat Soup -> Bool .
  op set? : Soup -> Bool .

--- Equality enrichments
  eq [7] : equ(N,N) = true .            --- Added for confluence
  eq [8] : equ(0,0) = true .
  eq [9] : equ(s(N),0) = false .
  eq [10] : equ(s(N),s(N')) = equ(N,N') .
--- ceq [eqenrich] : N = N' if equ(N,N') [nonexec] .

--- Soup membership
  eq [11] : in?(N,none) = false .
  eq [12] : in?(N, N' Si) = equ(N,N') or in?(N,Si) .

--- Some soups are sets 
  eq [13] : set?(none) = true .
 ceq [14] : set?(N Si) = true if in?(N,Si) = false /\ set?(Si) = true .
 ceq [15] : set?(N Si) = false if in?(N,Si) = true .
  eq [16] : set?(N N Si) = false .
endfm
