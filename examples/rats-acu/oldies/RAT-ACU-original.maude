---- Spec for crchc3o
---- In this version equations are completed for ACU coherence
---- Identities are removed using variants by the tool and then built-in ACU
---- unification is used for the generation of critical pairs

set include BOOL off .

fmod BOOL-FVP is
  sort Truth .
  ops tt ff : -> Truth [ctor] .
  op _/\_ : Truth Truth -> Truth .
  op _\/_ : Truth Truth -> Truth .
  op ~ : Truth -> Truth .
  vars B X Y Z : Truth .
  eq [bool01]: tt /\ B = B  [variant] .
  eq [bool02]: ff /\ B = ff [variant] .
  eq [bool03]: ff \/ B = B  [variant] .
  eq [bool04]: tt \/ B = tt [variant] .
  eq [bool05]: ~(tt) = ff [variant] .
  eq [bool06]: ~(ff) = tt [variant] .
endfm

fmod NAT-FVP is  protecting BOOL-FVP .
  sorts Nat NzNat Zero .
  subsorts Zero NzNat < Nat .
  op 0 : -> Zero [ctor] .
  op 1 : -> NzNat [ctor] .
  op _+_ : Nat Nat -> Nat [ctor assoc comm id: 0 prec 33] .
  ---- op _+_ : Nat NzNat -> NzNat [ctor ditto] .
  ---- op _+_ : NzNat Nat -> NzNat [ctor ditto] .
  op _+_ : NzNat NzNat -> NzNat [ctor ditto] .
  op p : NzNat -> Nat .                *** predecessor
  op max : Nat Nat -> Nat [comm] .
  op min : Nat Nat -> Nat [comm] .
  op d : Nat Nat -> Nat [comm] .       *** symmetric difference
  op _\_ : Nat Nat -> Nat .            *** monus
  op _~_ : Nat Nat -> Truth [comm] .   *** equality predicate
  op _>_ : Nat Nat -> Truth .

  vars N M : Nat .   vars N' M' K' : NzNat .

  eq [nat-fvp01]: p(N + 1) = N [variant] .
  eq [nat-fvp02]: max(N + M, N) = N + M [variant] .
  eq [nat-fvp03]: min(N + M, N) = N [variant] .
  eq [nat-fvp04]: d(N + M, N) = M [variant] .
  eq [nat-fvp05]: (N + M) \ N = M [variant] .
  eq [nat-fvp06]: N \ (N + M) = 0 [variant] .
  eq [nat-fvp07]: N ~ N = tt [variant] .
  eq [nat-fvp08]: (N + M') ~ N = ff [variant] .
  eq [nat-fvp09]: M + N + 1 > N = tt [variant] .
  eq [nat-fvp10]: N > N + M = ff [variant] .
endfm


fmod NAT-ACU is
  protecting NAT-FVP .
  op quot : Nat NzNat -> Nat .            *** quotient
  op gcd : NzNat NzNat -> NzNat [comm] .  *** greatest common divisor
  op _*_ : Nat Nat -> Nat [assoc comm prec 31] .
  op _*_ : NzNat NzNat -> NzNat [ditto] .

  vars N M : Nat .   vars N' M' K' : NzNat .  var N? : [Nat] .

  eq [nat-acu01]: quot(N' + M', M') = 1 + quot(N', M')  .
  eq [nat-acu02]: quot(M', M') = 1 .
  eq [nat-acu03]: quot(M, N' + M) = 0 .
  eq [nat-acu04]: gcd(N', N') = N' .
  eq [nat-acu05]: gcd(N' + M', M') = gcd(N', M') .
  eq [nat-acu06]: N? * 0 = 0 .
  eq [nat-acu07]: N? * 1 = N? .
  eq [nat-acu08]: N' * (M' + K') = (N' * M') + (N' * K') .
  ---- eq [nat-acu09]: N? * N' * (M' + K') = N? * ((N' * M') + (N' * K')) .  *** coherence extension
endfm


fmod INT-FVP is protecting NAT-FVP .
  sorts NzNeg NzInt Int .
  subsorts Nat NzNeg < Int .
  subsorts NzNat NzNeg < NzInt < Int .
  op - : NzNat -> NzNeg [ctor] .
  op - : Int -> Int .
  op _+_ : Int Int -> Int [ctor ditto] .
  op |_| : Int -> Nat .                *** absolute value
  op p : Int -> Int .                  *** predecessor
  op max : Int Int -> Int [comm] .
  op min : Int Int -> Int [comm] .
  op _~_ : Int Int -> Truth [comm] .   *** equality predicate
  op _>_ : Int Int -> Truth .

  vars I J : Int .   vars I' J' : NzInt .
  vars N' M' : NzNat .  var N : Nat .  ---- var I? : [Int] .

  ---- eq I? + N' + -(N') = I? [variant] .                  *** coherence extension
  ---- eq I? + -(N') + -(M') = I? + -(N' + M') [variant] .  *** coherence extension
  ---- eq I? + N' + -(N' + M') = I? + -(M') [variant] .     *** coherence extension
  eq -(0) = 0 [variant] .
  eq -(-(N')) = N' [variant] .
  eq | N | = N [variant] .
  eq | -(N') | = N' [variant] .
  eq p(0) = -(1) [variant] .
  eq p(-(N')) = -(N' + 1) [variant] .
  eq max(-(N'),-(M')) = -(min(N',M')) [variant] .
  eq max(-(N'),N) = N [variant] .
  eq min(-(N'),-(M')) = -(max(N',M')) [variant] .
  eq min(-(N'),N) = -(N') [variant] .
  eq -(N') ~ -(M') = N' ~ M' [variant] .
  eq -(N') ~ N = ff [variant] .
  eq -(N') > -(M') = M' > N' [variant] .
  eq -(N') > N = ff [variant] .
  eq N > -(N') = tt [variant] .
endfm


fmod INT-ACU is protecting INT-FVP .  protecting NAT-ACU .
  op quot : Int NzInt -> Int .
  op gcd : NzInt NzInt -> NzNat [comm] .
  op _*_ : Int Int -> Int [ditto] .
  op _*_ : NzInt NzInt -> NzInt [ditto] .

  vars I J : Int .   vars I' J' : NzInt .
  vars N' M' K' : NzNat .  var Q : NzNeg .  ---- var I? : [Int] .

  eq quot(0,Q) = 0 .
  eq quot(-(N'),M') = -(quot(N',M')) .
  eq quot(N',-(M')) = -(quot(N',M')) .
  eq quot(-(N'),-(M')) = quot(N',M') .
  eq gcd(-(N'),I') = gcd(N',I') .
  eq -(M') * -(N') = N' * M' .
  ---- eq I? * -(M') * -(N') = I? * N' * M' .  *** coherence extension
  eq M' * -(N') = -(N' * M') .
  ---- eq I? * M' * -(N') = I? * -(N' * M') .  *** coherence extension
endfm

fmod RAT-ACU is
  protecting INT-ACU .
  sorts Rat NzRat PosRat NzPosRat .
  subsort Int < Rat .
  subsorts Nat NzPosRat < PosRat < Rat .
  subsorts NzInt < NzRat < Rat .
  subsorts NzNat < NzPosRat < NzRat PosRat .
  op _/_ : NzRat NzRat -> NzRat .       *** division
  op _/_ : NzInt NzNat -> NzRat [ctor] .
  op _/_ : NzNat NzNat -> NzPosRat [ctor] .
  op - : Rat -> Rat .
  op _+_ : Rat Rat -> Rat [ditto] .
  op _+_ : PosRat PosRat -> PosRat [ditto] .
  op _*_ : Rat Rat -> Rat [ditto] .
  op _*_ : NzRat NzRat -> NzRat [ditto] .
  op |_| : Rat -> PosRat .             *** absolute value
  op max : Rat Rat -> Rat [comm] .
  op min : Rat Rat -> Rat [comm] .
  op _~_ : Rat Rat -> Truth [comm] .   *** equality predicate
  op _>_ : Rat Rat -> Truth .

  vars I' J' : NzInt .   vars R S : Rat .    var R? : [Rat] .
  vars R' S' : NzRat .   vars N' M' K' : NzNat .

  ceq [rat01]: J' / M' = quot(J',gcd(J',M')) / quot(M',gcd(J',M'))
     if gcd(J',M') > 1 = tt .                           *** canonical form of fraction
  eq [rat02]: R' / 1 = R' .
  eq [rat03]: I' / -(N') = -(I') / N' .
  eq [rat04]: I' / (J' / M') = (I' * M') / J' .
  eq [rat05]: (I' / N') / J' = I' / (N' * J') .
  eq [rat06]: (I' / N') / (J' / M') = (I' * M') / (N' * J') .
  ---- eq R? + I' + (J' / M') = R? + (((I' * M') + J') / M') .                      *** coherence extension
  ---- eq R? + (I' / N') + (J' / M') = R? + (((I' * M') + (J' * N')) / (N' * M')) . *** coherence extension
  eq [rat07]: -(I' / N') = -(I') / N' .
  eq [rat08]: (I' / N') * 0 = 0 .
  ---- eq R? * (I' / N') * 0 = 0 .                                     *** coherence extension
  eq [rat09]: (I' / N') * J' = (I' * J') / N' .
  ---- eq R? * (I' / N') * J' = R? * ((I' * J') / N') .                *** coherence extension
  eq [rat10]: (I' / N') * (J' / M') = (I' * J') / (N' * M') .
  ---- eq R? * (I' / N') * (J' / M') = R? * ((I' * J') / (N' * M')) .  *** coherence extension
  eq [rat11]: | I' / N' | = | I' | / N' .
  eq [rat12]: max(I' / N',J') = max(I',(J' * N')) / N' .
  eq [rat13]: max(I' / N',J' / M') = max((I' * M'),(J' * N')) / (N' * M') .
  eq [rat14]: min(I' / N',J') = min(I',(J' * N')) / N' .
  eq [rat15]: min(I' / N',J' / M') = min((I' * M'),(J' * N')) / (N' * M') .
  eq [rat16]: (I' / N') ~ J' = I' ~ (J' * N') .
  eq [rat17]: (I' / N') ~ (J' / M') = (I' * M') ~ (J' * N') .
  eq [rat18]: I' > (J' / M') = (I' * M') > J' .
  eq [rat19]: (I' / N') > J' = I' > (J' * N') .
  eq [rat20]: (I' / N') > J' = I' > (J' * N') .
  eq [rat21]: (I' / N') > (J' / M') = (I' * M') > (J' * N') .
endfm
