set include BOOL off .

fmod BOOL-FVP is
  sort Truth .
  ops tt ff : -> Truth [ctor] .
  op _/\_ : Truth Truth -> Truth .
  op _\/_ : Truth Truth -> Truth .
  op ~ : Truth -> Truth .
  vars B X Y Z : Truth .
  eq [BOOL-01]: tt /\ B = B  [variant] .
  eq [BOOL-02]: ff /\ B = ff [variant] .
  eq [BOOL-03]: ff \/ B = B  [variant] .
  eq [BOOL-04]: tt \/ B = tt [variant] .
  eq [BOOL-05]: ~(tt) = ff [variant] .
  eq [BOOL-06]: ~(ff) = tt [variant] .
endfm

fmod NAT-FVP is
  protecting BOOL-FVP .
  sorts Nat NzNat Zero .
  subsorts Zero NzNat < Nat .
  op 0 : -> Zero [ctor] .
  op 1 : -> NzNat [ctor] .
  op _+_ : Nat Nat -> Nat [ctor assoc comm id: 0] .
  op _+_ : NzNat NzNat -> NzNat [ctor assoc comm id: 0] .
  op p : NzNat -> Nat .                *** predecessor
  op max : Nat Nat -> Nat [comm] .
  op max : NzNat NzNat -> NzNat [comm] .
  op min : Nat Nat -> Nat [comm] .
  op min : NzNat NzNat -> NzNat [comm] .
  op d : Nat Nat -> Nat [comm] .       *** symmetric difference
  op _\_ : Nat Nat -> Nat .            *** monus
  op _~_ : Nat Nat -> Truth [comm] .   *** equality predicate
  op _>_ : Nat Nat -> Truth .

  vars N M : Nat .
  vars N' M' K' : NzNat .

  eq [NAT-FVP-01]: p(N + 1) = N [variant] .
  eq [NAT-FVP-02]: max(N + M, N) = N + M [variant] .
  eq [NAT-FVP-03]: min(N + M, N) = N [variant] .
  eq [NAT-FVP-04]: d(N + M, N) = M [variant] .
  eq [NAT-FVP-05]: (N + M) \ N = M [variant] .
  eq [NAT-FVP-06]: N \ (N + M) = 0 [variant] .
  eq [NAT-FVP-07]: N ~ N = tt [variant] .
  eq [NAT-FVP-08]: (N + M') ~ N = ff [variant] .
  eq [NAT-FVP-09]: M + N + 1 > N = tt [variant] .
  eq [NAT-FVP-10]: N > N + M = ff [variant] .
endfm

fmod NAT-ACU is
  protecting NAT-FVP .
  op quot : Nat NzNat -> Nat .            *** quotient
  op gcd : NzNat NzNat -> NzNat [comm] .  *** greatest common divisor
  op _*_ : Nat Nat -> Nat [assoc comm] .
  op _*_ : NzNat NzNat -> NzNat [assoc comm] .

  vars N M : Nat .
  vars N' M' K' : NzNat .
  var  N? : [Nat] .

  eq [NAT-ACU-01]: quot(N' + M', M') = 1 + quot(N', M')  .
  eq [NAT-ACU-02]: quot(M', M') = 1 .
  eq [NAT-ACU-03]: quot(M, N' + M) = 0 .
  eq [NAT-ACU-04]: gcd(N', N') = N' .
  eq [NAT-ACU-05]: gcd(N' + M', M') =  gcd(N', M') .
  eq [NAT-ACU-06]: N? * 0 = 0 .
  eq [NAT-ACU-07]: N? * 1 = N? .
  eq [NAT-ACU-08]: N' * (M' + K') = (N' * M') + (N' * K') .
endfm

fmod INT-FVP is
  protecting NAT-FVP .
  sorts NzNeg NzInt Int .
  subsorts Nat NzNeg < Int .
  subsorts NzNat NzNeg < NzInt < Int .
  op -_ : NzNat -> NzNeg [ctor] .
  op -_ : Int -> Int .
  op -_ : NzInt -> NzInt .
  op _+_ : NzInt NzInt -> NzInt [assoc comm id: 0] .
  op |_| : Int -> Nat .                *** absolute value
  op |_| : NzInt -> NzNat .
  op p : Int -> Int .                  *** predecessor
  op max : Int Int -> Int [comm] .
  op max : NzInt NzInt -> NzInt [comm] .
  op min : Int Int -> Int [comm] .
  op min : NzInt NzInt -> NzInt [comm] .
  op _~_ : Int Int -> Truth [comm] .   *** equality predicate
  op _>_ : Int Int -> Truth .

  vars I J : Int .
  vars I' J' : NzInt .
  vars N' M' : NzNat .
  var  N : Nat .
  var  I? : [Int] .

  eq [INT-FVP-01]: - 0 = 0 [variant] .
  eq [INT-FVP-02]: - - N' = N' [variant] .
  eq [INT-FVP-03]: | N | = N [variant] .
  eq [INT-FVP-04]: | - N' | = N' [variant] .
  eq [INT-FVP-05]: p(0) = - 1 [variant] .
  eq [INT-FVP-06]: p(- N') = -(N' + 1) [variant] .
  eq [INT-FVP-07]: max(- N', - M') = - min(N', M') [variant] .
  eq [INT-FVP-08]: max(- N', N) = N [variant] .
  eq [INT-FVP-09]: min(- N', - M') = - max(N', M') [variant] .
  eq [INT-FVP-10]: min(- N', N) = - N' [variant] .
  eq [INT-FVP-11]: - N' ~ - M' = N' ~ M' [variant] .
  eq [INT-FVP-12]: - N' ~ N = ff [variant] .
  eq [INT-FVP-13]: - N' > - M' = M' > N' [variant] .
  eq [INT-FVP-14]: - N' > N = ff [variant] .
  eq [INT-FVP-15]: N > - N' = tt [variant] .
endfm

fmod INT-ACU is
  protecting INT-FVP .
  protecting NAT-ACU .
  op quot : Int NzInt -> Int .
  op gcd : NzInt NzInt -> NzNat [comm] .
  op _*_ : Int Int -> Int [assoc comm] .
  op _*_ : NzInt NzInt -> NzInt [assoc comm] .

  vars I J : Int .
  vars I' J' : NzInt .
  vars N' M' K' : NzNat .
  var  Q : NzNeg .
  var  I? : [Int] .

  eq [INT-ACU-01]: quot(0, Q) = 0 .
  eq [INT-ACU-02]: quot(- N', M') = - quot(N', M') .
  eq [INT-ACU-03]: quot(N', - M') = - quot(N', M') .
  eq [INT-ACU-04]: quot(- N', - M') = quot(N', M') .
  eq [INT-ACU-05]: gcd(- N', I') = gcd(N', I') .
  eq [INT-ACU-06]: - M' * - N' = N' * M' .
  eq [INT-ACU-07]: M' * - N' = - (N' * M') .
endfm

fmod RAT-ACU is
  protecting INT-ACU .
  sorts Rat NzRat PosRat NzPosRat .
  subsort Int < Rat .
  subsorts Nat NzPosRat < PosRat < Rat .
  subsorts NzInt < NzRat < Rat .
  subsorts NzNat < NzPosRat < NzRat PosRat .
  op _/_ : NzRat NzRat -> NzRat .       *** division
  op _/_ : NzInt NzNat -> NzRat [ctor] .
  op _/_ : NzNat NzNat -> NzPosRat [ctor] .
  op -_ : Rat -> Rat .
  op _+_ : Rat Rat -> Rat [assoc comm id: 0] .
  op _+_ : PosRat PosRat -> PosRat [assoc comm id: 0] .
  op _*_ : Rat Rat -> Rat [assoc comm] .
  op _*_ : NzRat NzRat -> NzRat [assoc comm] .
  op |_| : Rat -> PosRat .             *** absolute value
  op max : Rat Rat -> Rat [comm] .
  op min : Rat Rat -> Rat [comm] .
  op _~_ : Rat Rat -> Truth [comm] .   *** equality predicate
  op _>_ : Rat Rat -> Truth .

  vars I' J' : NzInt .
  vars R S : Rat .
  var  R? : [Rat] .
  vars R' S' : NzRat .
  vars N' M' K' : NzNat .

 ceq [RAT-ACU-01]: J' / M' = quot(J', gcd(J', M')) / quot(M', gcd(J', M'))
     if gcd(J', M') > 1 = tt .                   *** canonical form of fraction
  eq [RAT-ACU-02]: R' / 1 = R' .
  eq [RAT-ACU-03]: I' / - N' = - I' / N' .
  eq [RAT-ACU-04]: I' / (J' / M') = (I' * M') / J' .
  eq [RAT-ACU-05]: (I' / N') / J' = I' / (N' * J') .
  eq [RAT-ACU-06]: (I' / N') / (J' / M') = (I' * M') / (N' * J') .
  eq [RAT-ACU-07]: - (I' / N') = - I' / N' .
  eq [RAT-ACU-08]: (I' / N') * 0 = 0 .
  eq [RAT-ACU-09]: (I' / N') * J' = (I' * J') / N' .
  eq [RAT-ACU-10]: (I' / N') * (J' / M') = (I' * J') / (N' * M') .
  eq [RAT-ACU-11]: | I' / N' | = | I' | / N' .
  eq [RAT-ACU-12]: max(I' / N', J') = max(I', J' * N') / N' .
  eq [RAT-ACU-13]: max(I' / N', J' / M') = max(I' * M', J' * N') / (N' * M') .
  eq [RAT-ACU-14]: min(I' / N', J') = min(I', J' * N') / N' .
  eq [RAT-ACU-15]: min(I' / N', J' / M') = min(I' * M', J' * N') / (N' * M') .
  eq [RAT-ACU-16]: (I' / N') ~ J' = I' ~ (J' * N') .
  eq [RAT-ACU-17]: (I' / N') ~ (J' / M') = (I' * M') ~ (J' * N') .
  eq [RAT-ACU-18]: I' > (J' / M') = (I' * M') > J' .
  eq [RAT-ACU-19]: (I' / N') > J' = I' > (J' * N') .
  eq [RAT-ACU-20]: (I' / N') > J' = I' > (J' * N') .
  eq [RAT-ACU-21]: (I' / N') > (J' / M') = (I' * M') > (J' * N') .
endfm

set include BOOL on .
