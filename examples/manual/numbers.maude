---- from the book All About Maude
---- from the Maude Manual
---- by the Maude team

set include BOOL off .

set include TRUTH-VALUE on .

fmod NUMBERS is
  ----protecting BOOL .

  sort Zero .
  sorts Nat NzNat .
  subsort Zero NzNat < Nat .
  op zero : -> Zero [ctor] .
  op s_ : Nat -> NzNat [ctor] .
  op sd : Nat Nat -> Nat .
  ops _+_ _*_ : Nat Nat -> Nat [assoc comm] .
  op _+_ : NzNat Nat -> NzNat [ditto] .
  op _*_ : NzNat NzNat -> NzNat [ditto] .
  op p : NzNat -> Nat .

  vars I N M : Nat .
  vars N' M' : Nat .
  eq N + zero = N .
  eq N + s M = s (N + M) .
  eq sd(N, N) = zero .
  eq sd(N, zero) = N .
  eq sd(zero, N) = N .
  eq sd(s N, s M) = sd(N, M) .
  eq N * zero = zero .
  eq N * s M = (N * M) + N .
  eq p(s N) = N [label partial-predecessor] .

  eq (N + M) * I = (N * I) + (M * I)
    [nonexec metadata "distributive law"] .

  sort Nat3 .
  ops 0 1 2 : -> Nat3 [ctor] .
  op _+_ : Nat3 Nat3 -> Nat3 [comm] .
  vars N3 : Nat3 .
  eq N3 + 0 = N3  .
  eq 1 + 1 = 2 .
  eq 1 + 2 = 0 .
  eq 2 + 2 = 1 .

  sort NatSeq .
  subsort Nat < NatSeq .
  op nil : -> NatSeq [ctor] .
  op __ : NatSeq NatSeq -> NatSeq [ctor assoc id: nil] .

  sort NatSet .
  subsort Nat < NatSet .
  op empty : -> NatSet [ctor] .
  op _;_ : NatSet NatSet -> NatSet [ctor assoc comm id: empty] .
  eq N ; N = N [label natset-idem] .

  op _in_ : Nat NatSet -> Bool .
  var NS : NatSet .
  ---- owise not supported by the MFE tools
  ---- eq N in N ; NS = true .
  ---- eq N in NS = false [owise] .
  eq N in M ; NS = N == M or N in NS .
  eq N in empty = false .

  ---- owise not supported by the MFE tools, _==_ needed
----  op _==_ : Nat Nat -> Bool [comm] .
----  eq N == N = true .
----  eq zero == N' = false .
----  eq s N == s M = N == M .
----
----  op _or_ : Bool Bool -> Bool [comm] .
----  var B : Bool .
----  eq true or B = true .
----  eq false or B = B .
  op _==_ : Nat Nat -> Bool .
  eq N == N = true .
  eq zero == N' = false .
  eq N' == zero = false .
  eq s N == s M = N == M .

  op _or_ : Bool Bool -> Bool .
  var B : Bool .
  eq true or B = true .
  eq false or B = B .
endfm
