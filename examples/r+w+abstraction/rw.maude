                                                --- Example: Readers and Writers
                                                --- Source:  All About Maude ...
                                                --- Ref.: Sections 12.3 and 12.4

(set include BOOL off .)
(set include TRUTH-VALUE on .)

(fmod MBOOL is
   sort MBool .
   ops true false : -> MBool [ctor] .
 endfm) 

(fmod MNAT is
   sort MNat .
   op 0 : -> MNat [ctor] .
   op s : MNat -> MNat [ctor] .
 endfm) 

(mod READERS-WRITERS is
   pr MNAT .
   
   sort Config .
   op <_`,_> : MNat MNat -> Config [ctor] .   --- readers/writers

   vars R W : MNat .

   rl [wrt+] : < 0, 0 > => < 0, s(0) > .
   rl [wrt-] : < R, s(W) > => < R, W > .
   rl [rdr+] : < R, 0 > => < s(R), 0 > .
   rl [rdr-] : < s(R), W > => < R, W > .
 endm)

(select tool MTT .)
---(
The MTT has been set as current tool.
---)

(ct READERS-WRITERS .)
---(
Success: The module READERS-WRITERS is terminating.
---)

(select tool CRC .)
---(
The CRC has been set as current tool.
---)

(ccr READERS-WRITERS .)
---(
Church-Rosser check for READERS-WRITERS
    There are no critical pairs.
    The specification is confluent.
    The module is sort-decreasing.
    The module is therefore Church-Rosser.
---)

(select tool ChC .)
---(
The ChC has been set as current tool.
---)

(cch READERS-WRITERS .)
---(
Coherence checking of READERS-WRITERS
    All critical pairs have been rewritrueen and no rewrite with rules can happen at non-overlapping positions of equations left-hand sides.
    The termination and Church-Rosser properties must still be checked.
---)

(submit .)
---(
The Church-Rosser goal for READERS-WRITERS has been submitted to the CRC
The termination goal for the functional part of READERS-WRITERS has been
    submitted to the MTT
The MTT proof for READERS-WRITERS has been reset.                               ---- why?
Success: The functional part of module READERS-WRITERS is terminating.
Church-Rosser check for READERS-WRITERS
    There are no critical pairs.
    The specification is confluent.
    The module is sort-decreasing.
    The module is therefore Church-Rosser.
The functional part of module READERS-WRITERS has been checked terminating.
The module READERS-WRITERS has been checked Church-Rosser.
Success: The module READERS-WRITERS is coherent.
---)

---- TODO: the module is not coherent yet in MFE, as sufficient completeness
---- has not yet been proved. Nevertheless, a success message is shown to
---- the user

(select tool SCC .)
---(
The SCC has been set as current tool.
---)

(SCC select READERS-WRITERS .)
---(
READERS-WRITERS is now the current module.
---)

(submit .)
---(
The sort-decreasingness goal for READERS-WRITERS has been submitted to CRC.
The termination goal for the functional part of READERS-WRITERS has been submitted to MTT.
Church-Rosser check for READERS-WRITERS
	The module is sort-decreasing.
Success: The functional module READERS-WRITERS is sufficiently complete and has free constructors.
The module READERS-WRITERS has been checked sufficiently-complete.
Success: The module READERS-WRITERS is coherent.     --- why again? see line 83
---)

(mod READERS-WRITERS-PREDS is
   pr MBOOL .
   pr READERS-WRITERS .

   ops mutex one-writer : Config -> MBool [frozen] .

   vars M N : MNat .

   eq mutex(< s(N), s(M) >) = false .
   eq mutex(< 0, N >) = true .
   eq mutex(< N, 0 >) = true .
   eq one-writer(< N, s(s(M)) >) = true .
   eq one-writer(< N, s(0) >) = true .
   eq one-writer(< N, 0 >) = true .
 endm)

(mod READERS-WRITERS-ABS is
   inc READERS-WRITERS-PREDS .
   var N : MNat .
   eq [abs] : < s(s(N)), 0 > = < s(0), 0 > .
 endm)

(select tool SCC .)
---(
The SCC has been set as current tool.
---)

(scc READERS-WRITERS-ABS .)
---(
Sufficient completeness check for READERS-WRITERS-ABS
	Completeness counter-examples: none were found
	Freeness counter-examples: < s(s(0)),0 > with sort Config
	Analysis: it is complete and it is sound
	Ground weak termination: not proved
	Ground sort-decreasingness: not proved
---)

(submit .)
---(
The sort-decreasingness goal for READERS-WRITERS-ABS has been submitted to CRC.
The termination goal for the functional part of READERS-WRITERS-ABS has been submitted to MTT.
Success: The functional part of module READERS-WRITERS-ABS is terminating.
Success: The functional module READERS-WRITERS-ABS is sufficiently complete.
---)

(select tool ChC .)
---(
The ChC has been set as current tool.
---)

(check ground coherence READERS-WRITERS-ABS .) ---- Coherence check for READERS-WRITERS-ABS
---(
Ground coherence checking of READERS-WRITERS-ABS
The following critical pairs cannot be rewritten:
  cp READERS-WRITERS-ABS1 for abs and rdr-
    < s(0),0 >
    => < s(#1:MNat),0 > .
The termination and Church-Rosser properties must still be checked.
---)

----set trace on .
set trace select on .
trace select genToITPMsg .

(submit .)            ---- the ChC submits all proof obligations 
---(
The Church-Rosser goal for READERS-WRITERS-ABS has been submitted to the CRC
The goal for critical pair READERS-WRITERS-ABS1 has been submitted to the ITP.
The termination goal for the functional part of READERS-WRITERS-ABS has been
    submitted to the MTT
Success: The functional part of module READERS-WRITERS-ABS is terminating.
The functional part of module READERS-WRITERS-ABS has been checked terminating.
---)

(select tool ITP .)
(ind* on #1:MNat .)

(select tool CRC .)
---(
The CRC has been set as current tool.
---)

(submit .) 
---(
The termination goal for the functional part of READERS-WRITERS-ABS has been submitted to the MTT
The functional part of module READERS-WRITERS-ABS has been checked terminating.
The module READERS-WRITERS-ABS has been checked Church-Rosser.
Success: The module READERS-WRITERS-ABS is ground-coherent.
---)

(search in READERS-WRITERS-ABS :
< 0, 0 > =>* C:Config such that mutex(C:Config) = false .)
---(
No solution.
---)

(search in READERS-WRITERS-ABS :
< 0, 0 > =>* C:Config such that one-writer(C:Config) = false .)
---(
No solution.
---)
